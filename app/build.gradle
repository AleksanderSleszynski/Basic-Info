apply plugin: 'com.android.application'
apply plugin: 'com.github.triplet.play'
apply plugin: 'android-apt'

apply from: '../incrementAndExport.gradle'

loadSecrets()

play {
    serviceAccountEmail = safeGet('PLAY_EMAIL')
    pk12File            = safeGetFile('PLAY_KEY')
}

String getVerName() {
    getVersionName()
}

Integer getVerCode() {
    getVersionCode() as Integer
}

android {
    compileSdkVersion 25
    buildToolsVersion '25.0.0'

    defaultConfig {
        applicationId "com.meedamian.info"
        minSdkVersion 18
        targetSdkVersion 25

        versionCode getVerCode()
        versionName getVerName()
    }

    dataBinding {
        enabled = true
    }


    compileOptions {
        sourceCompatibility JavaVersion.VERSION_1_7
        targetCompatibility JavaVersion.VERSION_1_7
    }

    signingConfigs {
        release {
            storeFile       safeGetFile('STORE_FILE') as File
            storePassword   safeGet('STORE_PASSWORD') as String
            keyAlias        safeGet('KEY_ALIAS') as String
            keyPassword     safeGet('KEY_PASSWORD', safeGet('STORE_PASSWORD')) as String
        }
    }

    buildTypes {
        debug {
            versionNameSuffix "-SNAPSHOT"
        }
        release {
            proguardFiles getDefaultProguardFile('proguard-android.txt'), 'proguard-rules.pro'
            signingConfig signingConfigs.release as Object
            minifyEnabled true
            zipAlignEnabled true
            shrinkResources true
        }
    }
}

dependencies {
    compile fileTree(dir: 'libs', include: ['*.jar'])
    testCompile 'junit:junit:4.12'
    apt 'com.github.hotchemi:permissionsdispatcher-processor:2.1.3'

    compile 'com.github.hotchemi:permissionsdispatcher:2.1.3'
    compile 'com.android.support:appcompat-v7:25.0.0'
    compile 'com.android.support:design:25.0.0'
    compile 'com.google.android.gms:play-services-location:9.8.0'
    compile 'com.google.android.gms:play-services-maps:9.8.0'
    compile 'com.squareup.retrofit2:retrofit:2.1.0'
    compile 'com.squareup.retrofit2:converter-gson:2.1.0'
    compile 'org.jetbrains:annotations-java5:15.0'
}
repositories {
    mavenCentral()
}
